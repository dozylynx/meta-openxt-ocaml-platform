SECTION = "devel"
LICENSE = "LGPLv2.1"
LIC_FILES_CHKSUM = "file://LICENSE;md5=4f72f33f302a53dc329f4d3819fe14f9"

SRC_URI = "http://caml.inria.fr/pub/distrib/ocaml-4.05/ocaml-4.05.0.tar.gz \
	   file://config.patch \
	   file://ocamlc-wrapper.sh \
"
SRC_URI[md5sum] = "89fd8cee148a00a6fcd5f587a2cebe10"
SRC_URI[sha256sum] = "7039bf3325bae8936c55f41b0e05df4a53d0f957cf1adc3d44aa0154c88b104e"

#FIXME: check PACKAGE_ARCH
PACKAGE_ARCH = "${MACHINE_ARCH}"

BBCLASSEXTEND = "native"

FLESEXTRAPATHS_prepend := "${THISDIR}/ocaml:"

S = "${WORKDIR}/ocaml-${PV}"

FILES_${PN}-staticdev += " ${libdir}/ocaml/vmthreads/*.a"

do_configure() {
    ./configure \
        -no-curses \
        -prefix ${prefix} \
        -cc "${CC}" \
        -mksharedlib "${CCLD} -shared" \
        -as "${AS}" -aspp "${CC} -c"

	sed -i'' -re 's/-lX11//' config/Makefile

    # Make the generated bytecode use 'env' to locate the ocamlrun interpreter
    # instead of using a hardcoded path
    sed -e 's,#!$(BINDIR)/ocamlrun,#!/usr/bin/env ocamlrun,' \
        -i'' stdlib/Makefile
    sed -e 's,#!$(TARGET_BINDIR)/ocamlrun,#!/usr/bin/env ocamlrun,' \
        -i'' stdlib/Makefile
}

do_compile() {
    # Later stages of the build exec bytecode output that was built earlier.
    # ocamlrun, which has been built by that point, needs to be in the PATH
    # for that to work.
    export PATH="${PATH}:${S}/boot"

	oe_runmake PREFIX="${prefix}" world.opt
}

do_install() {
	make PREFIX="${D}${prefix}" install
}

do_package_write_ipk[noexec] = "1"
do_package_write_rpm[noexec] = "1"
do_package_write_deb[noexec] = "1"

INHIBIT_SYSROOT_STRIP = "1"
INHIBIT_PACKAGE_STRIP = "1"

# There is a dependency bug in the doc build that causes failure in parallel builds.
# Note that disabling parallel builds significantly increases build time.
PARALLEL_MAKE = ""
